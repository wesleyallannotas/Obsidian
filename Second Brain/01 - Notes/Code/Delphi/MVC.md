---
title: MVC no Delphi
author: Wesley Silva
url:
book:
type: note
completed: false
aliases:
tags: [delphi]
description: Como utilizar a arquitetura MVC no delphi 
---
#  üöÄ Introdu√ß√£o
MVC basicamente √© a divis√£o do nosso software entre 3 camadas, sendo elas _Model_, _VIew_ e _Controller_, sua ideia √© b√°sica, porem sua implementa√ß√£o requer um conhecimento e alto racioc√≠nio. 
- _Model_ - Respons√°vel pelo acesso aos dados, valida√ß√£o dos dados, basicamente implementa a regra de negocio
- _Controller_ - Respons√°vel por realizar o meio de campo, ligando o _view_ com o _model_
- _View_ - A interface do usu√°rio, onde o mesmo interage com a aplica√ß√£o, tratando de _backend_, a _view_ seria a URL com os dados em si.

Em uma analogia com um restaurante, o menu seria nossa _view_, onde temos acesso as requisi√ß√µes que podemos realizar,  quando escolhemos enviamos a solicita√ß√£o para o gar√ßom que √© o _controller_, que entrega nosso pedido para o cozinheiro que √© o _Model_, e depois volta do cozinheiro para o gar√ßom e cliente.

_Model_ - Entidades, Conex√£o com o Banco de Dados, Datasets.
_Controller_ - Atrav√©s da requisi√ß√£o da _View_ defini as configura√ß√µes da conex√£o, n√≠vel de acesso, tabelas a serem consultadas.
_View_ - Requisi√ß√µes do usu√°rio sobre o que deseja acessar.

_View_ chama fabrica do _Controller_ que se necess√°rio chama fabrica do _Model_

# Nomenclatura
Uma convec√ß√£o usada para nomea√ß√£o de `units` em projetos que utiliza a arquitetura MVC √© `nome do projeto + camada + e o nome em si`
Pro exemplo, `Calculadora.View.Main.pas`, e dividiremos as camadas em pastas separadas.

## Factory
Basicamente s√£o fabricas de constru√ß√£o para reduzira acoplamentos entre as `units` e criar elementos .

```pascal
interface

uses
  Menus.Controller.Interfaces, Menus.Controller.ListBox.Itens.Default;

type
  TControllerListBoxItensFactory = class(TInterfacedObject, IControllerListBoxItensFactory)
    constructor Create;
    destructor Destroy; override;
    class function New : IControllerListBoxItensFactory;
    function Default : IControllerListBoxItensDefault;
  end;

implementation

{ TControllerListBoxItensFactory }

constructor TControllerListBoxItensFactory.Create;
begin
end;

function TControllerListBoxItensFactory.Default: IControllerListBoxItensDefault;
begin
  Result := TControllerListBoxItensDefault.New;
end;

destructor TControllerListBoxItensFactory.Destroy;
begin
  inherited;
end;

class function TControllerListBoxItensFactory.New: IControllerListBoxItensFactory;
begin
  Result := Self.Create;
end;

end.

```

## Classes Default
Criamos uma classe _default_ que prove o necess√°rio para criar o que √© necess√°rio, porem sem utilizar de heran√ßa, basicamente um elemento base. Linkamos esse elemento _default_ com sua [[#Factory]], onde a partir da instancia√ß√£o da _factory_ e de um _default_ dentro dela podemos criar novos itens.

```pascal
unit Menus.Controller.ListBox.Itens.Default;

interface

uses Menus.Controller.Interfaces, FMX.ListBox, FMX.Types, System.Classes;

type
  TControllerListBoxItensDefault = class(TInterfacedObject, IControllerListBoxItensDefault)
  private
    FListBoxItem : TListBoxItem;
  public
    constructor Create;
    destructor Destroy; override;
    class function New : IControllerListBoxItensDefault;
    function Name(Value : string) : IControllerListBoxItensDefault;
    function Text(Value : string) : IControllerListBoxItensDefault;
    function StyleLookup(Value : string) : IControllerListBoxItensDefault;
    function OnClick(Value : TNotifyEvent): IControllerListBoxItensDefault;
    function Item : TFMXObject;
  end;

implementation

{ TControllerListBoxItensDefault }

constructor TControllerListBoxItensDefault.Create;
begin
  FListBoxItem := TListBoxItem.Create(nil);
  FListBoxItem.Name := 'btnDefault';
  FListBoxItem.Text := 'Default';
  FListBoxItem.StyleLookup := 'listboxitemdetaillabel';
end;

destructor TControllerListBoxItensDefault.Destroy;
begin
  inherited;
end;

function TControllerListBoxItensDefault.Item: TFMXObject;
begin
  Result := FListBoxItem;
end;

function TControllerListBoxItensDefault.Name(
  Value: string): IControllerListBoxItensDefault;
begin
  FListBoxItem.Name := value;
  Result := Self;
end;

class function TControllerListBoxItensDefault.New: IControllerListBoxItensDefault;
begin
  Result := Self.Create;
end;

function TControllerListBoxItensDefault.OnClick(
  Value: TNotifyEvent): IControllerListBoxItensDefault;
begin
  FListBoxItem.OnClick := Value;
end;

function TControllerListBoxItensDefault.StyleLookup(
  Value: string): IControllerListBoxItensDefault;
begin
  FListBoxItem.StyleLookup := value;
  Result := Self;
end;

function TControllerListBoxItensDefault.Text(
  Value: string): IControllerListBoxItensDefault;
begin
  FListBoxItem.Text := value;
  Result := Self;
end;

end.
```

Em seguida criamos um interface comum para os itens n√£o padr√µes.

```pascal
  IControllerListBoxItemForm = interface
    // CTRL + SHIFT + G
    function Show : TFMXObject;
  end;
```

Assim sendo o _Defaul_ faz o trabalho pesado, e os advindos dela realizam apenas a especializa√ß√£o.
Basicamente desenvolvemos as _factorys_, ou fabricas, onde iram conhecer todos que elas podem criar, criamos o _default_ que vai fazer o real trabalho duro, e os subsequentes utilizara do _default_ aplicando algumas altera√ß√µes.
Onde ==nossa _view_ enxergara apenas nossa _Factory_.==

>[!tip] Viola√ß√£o de Acesso
>Pode ser falta de um `Result := Self` em algum m√©todo, ou esquecer de instanciar alguma classe, em 99,9% das vezes que da erro de _acess violation_ na programa√ß√£o funcional √© esse o problema.

## Desacoplando Formul√°rios
Podemos desacoplar o conhecimento de nossos _itens_ do _ListBox_ do formul√°rio, basicamente criamos uma classe que criara o formul√°rio o chama e o limpa da memoria, atrav√©s do nome do mesmo que ser√° passado como _string_.

>[!Tip] Trocando IF por try...except
>Antes o c√≥digo verificava se a vari√°vel n√£o era nula e devolvia um erro.
>```pascal
objFMX :=  TFmxObjectClass(GetClass(ClassName));
if objFMX <> nil then
>	newForm := TCustomForm(objFMX)
else
>	raise Exception.Create('Objeto n√£o existe');
>	
>try
>	newForm.ShowModal;
>finally
>	newForm.Free;
end;
>```
>Porem nos podemos substituir esse `if` por um `try...exception` ficando da seguinte forma.

```pascal
unit Menus.Controller.Forms.Default;

interface

type
  TControllerFormDefault = class
    class procedure CreateForm(ClassName : string);
  end;

implementation

{ TControllerFormDefault }

uses
  FMX.Types, System.Classes, FMX.Forms, System.SysUtils;

class procedure TControllerFormDefault.CreateForm(ClassName: string);
var
  ObjFMX : TFmxObjectClass;
  newForm : TCustomForm;
begin
  objFMX :=  TFmxObjectClass(GetClass(ClassName));

  try
    newForm := (objFMX.Create(nil) as TCustomForm);
    try
      newForm.Position := TFormPosition.ScreenCenter;
      newForm.ShowModal;
    finally
      newForm.Free;
    end;
  except
    on E : Exception do
      raise Exception.Create(E.ClassName + E.Message);
  end;
end;
end.
```

# Model
_Model_ e a camada que lida com os dados, como sabemos √© de extrema import√¢ncia criar um sistema male√°vel que aceitem mudan√ßas facilmente, por isso, criamos uma estrutura que permita independente do componente de conex√£o utilizado, diminuindo o acoplamento.

```pascal
type
  IModelConnection = interface
    ['{0BD0485F-506E-4A4F-97D4-CA2AAC2D6260}']
    function EndConnection : TComponent;
  end;
```

## Firedac
Vamos implementar essa interface utilizando o FireDAC com o componente de conex√£o, por padr√£o vamos utilizar o _FireBird_ existem formas de desacoplar o banco de dados tamb√©m, mas n√£o ser√° nesse momento.

```pascal
unit Menus.Model.Connections.ConnectFiredac;

interface

uses
  Menus.Model.Connections.Intefaces, System.Classes, FireDAC.Comp.Client,
  FireDAC.Comp.UI, FireDAC.Phys.FB;

type
  TModelConnectionFiredac = class(TInterfacedObject, IModelConnection, IModelConnectionParams)
  private
    FConnection : TFDConnection;
    FDGUIxWaitCursor : TFDGUIxWaitCursor;
    FDPhysFBDriverLink : TFDPhysFBDriverLink;
    FDataBase : string;
    FUserName : string;
    FPassword : string;
    FDriverID : string;
    FServer : string;
    FPorta : Integer;
    procedure ReadParameters;
  public
    constructor Create;
    destructor Destroy; override;
    class function New : IModelConnection;
    function EndConnection : TComponent;
    function Params : IModelConnectionParams;
    function DataBase(Value : string) : IModelConnectionParams;
    function UserName(Value : string) : IModelConnectionParams;
    function Password(Value : string) : IModelConnectionParams;
    function DriverID(Value : string) : IModelConnectionParams;
    function Server(Value : string) : IModelConnectionParams;
    function Porta(Value : Integer) : IModelConnectionParams;
    function EndParams : IModelConnection;
  end;

implementation

uses
  System.SysUtils;

{ TModelConnectionFiredac }

constructor TModelConnectionFiredac.Create;
begin
  FConnection := TFDConnection.Create(nil);
  FDGUIxWaitCursor := TFDGUIxWaitCursor.Create(nil);
  FDPhysFBDriverLink := TFDPhysFBDriverLink(nil);
  ReadParameters;
  FConnection.Connected := True;
end;

function TModelConnectionFiredac.DataBase(
  Value: string): IModelConnectionParams;
begin
  FDataBase := Value;
  Result := Self;
end;

destructor TModelConnectionFiredac.Destroy;
begin
  FConnection.Free;
  FDGUIxWaitCursor.Free;
  FDPhysFBDriverLink.Free;
  inherited;
end;

function TModelConnectionFiredac.DriverID(
  Value: string): IModelConnectionParams;
begin
  FDriverID := Value;
  Result := Self;
end;

function TModelConnectionFiredac.EndConnection: TComponent;
begin
  Result := FConnection;
end;

function TModelConnectionFiredac.EndParams: IModelConnection;
begin
  Result := Self;
end;

procedure TModelConnectionFiredac.ReadParameters;
begin
  FConnection.Params.Database := FDataBase;
  FConnection.Params.UserName := FUserName;
  FConnection.Params.Password := FPassword;
  FConnection.Params.DriverID := FDriverID;
  FConnection.Params.Add('Server=' + FServer);
  FConnection.Params.Add('Port=' + IntToStr(FPorta));
end;

class function TModelConnectionFiredac.New: IModelConnection;
begin
  Result := Self.Create;
end;

function TModelConnectionFiredac.Params: IModelConnectionParams;
begin
  Result := Self;
end;

function TModelConnectionFiredac.Password(
  Value: string): IModelConnectionParams;
begin
  FPassword := Value;
  Result := Self;
end;

function TModelConnectionFiredac.Porta(Value: Integer): IModelConnectionParams;
begin
  FPorta := Value;
  Result := Self;
end;

function TModelConnectionFiredac.Server(Value: string): IModelConnectionParams;
begin
  FServer := Value;
  Result := Self;
end;

function TModelConnectionFiredac.UserName(
  Value: string): IModelConnectionParams;
begin
  FUserName := Value;
  Result := Self;
end;

end.
```

## Querys
Como estamos trabalhando independente da camada de dados, ser√° necess√°rio desenvolver uma forma gen√©rica para n√£o ficarmos presos e acoplados  com uma depend√™ncia na camada de _view_ especifico.
Teremos que criar `units` especificas para cada tipo de componente de conex√£o utilizado, por exemplo o FireDAC.

```pascal
unit Menus.Model.Connections.TableFiredac;

interface

uses
  Menus.Model.Connections.Intefaces, FireDAC.Comp.Client, System.Classes;

type
  TModelConnectionsTableFireDac = class(TInterfacedObject, IModelDataSet)
  private
    FTable : TFDTable;
  public
    constructor Create(Connection : IModelConnection);
    destructor Destroy; override;
    class function New(Connection : IModelConnection) : IModelDataSet;
    function Open(aTable : string) : IModelDataSet;
    function EndDataSet : TComponent;
  end;

implementation

{ TModelConnectionsTableFireDac }

constructor TModelConnectionsTableFireDac.Create(Connection : IModelConnection);
begin
  FTable := TFDTable.Create(nil);
  FTable.Connection := (Connection.EndConnection as TFDConnection);
end;

destructor TModelConnectionsTableFireDac.Destroy;
begin
  FTable.Free;
  inherited;
end;

function TModelConnectionsTableFireDac.EndDataSet: TComponent;
begin
  Result := FTable;
end;

class function TModelConnectionsTableFireDac.New(Connection : IModelConnection) : IModelDataSet;
begin
  Result := Self.Create(Connection);
end;

function TModelConnectionsTableFireDac.Open(aTable: string): IModelDataSet;
begin
  FTable.Open(ATable);
  Result := Self;
end;

end.
```

ssim sendo caso lance um novo componente de conex√£o basta mudar ConnectFiredac e TableFiredac para utilizar os novos compoentnes e adequando o nome dos atributos para os mesmo.
Tamb√©m temos a op√ß√£o de desenvolver novas `units`.

## Factorys sem Default
Pode ocorrer casos de _factorys_ sem uma classe Default, normalmente quando cada componente/classe que ela gere possua diferentes significativas na estrutura, como uma _factory_ para conex√µes com o banco de dados e de Datasets, dependendo das diferen√ßas das classes/datases n√£o tem com seguir um default.

## Interface Para Entidades
Entendendo entidades como tabelas do banco de dados, √© interessante criar uma _interface_ gen√©rica com o b√°sico que toda entidade deve ter e a partir dela criar as especificas.

>[!attention] Aten√ß√£o
>Precisamos de um DataSet no Entity para conectar com o banco, ai vem a duvida criamos o mesmo com a _factory_ dentro da Entity, N√ÉO, importante frisar quem CONTROLA √© o _Controller_, ele que defini essas coisas, por isso criaremos _factorys_ no _controller_ para criar conex√µes e _datasets_.

No _Controller_ utilizaremos o _DataSource_ na tipagem, pois, √© o mais gen√©rico e utilizado como base para receber o _DataSets_ e outros, assim sendo compat√≠vel com todos.
O uso de classes gen√©ricas √© a base para o bom funcionamento com todos os tipos de componentes de conex√£o diferentes.

## Criando Novas Conex√µes
Utilizando outros componentes como por exemplo o Zeus, precisar√≠amos apenas criar novas classes para a Conex√£o e para o _DataSet_, modificando os mesmo para serem compotiveis com Zeus ou qualquer outro, posterior mente adicionar nas _Factorys_ e utiliza-los.

# View
Na nossa _view_ precisamos apenas de um _DataSorce_, e um atributo privado que contenha nossa entidade utilizando a _interface_ `IControllerEntity`.
Por exemplo utilizamos um _StringGrid_ ent√£o fizemos a conex√£o via `Live Bind`.